config {
  type: "assertion",
  description: "Verifies WF v4 SL decomposition is consistent: clinical + nc + pharma + food + exclude + unmatched should equal total_spend per system (within tolerance). Small gaps are expected because entity_sl_mix percentages may not sum to exactly 100% (UNKNOWN categories excluded by INNER JOIN to service_line_mapping). A gap >0.1% of total_spend indicates a classification logic issue.",
  tags: ["assertion", "reconciliation"],
  dependencies: ["wf_sl_v4"]
}

-- Returns systems where SL breakdown doesn't reconcile to total within 0.1% of system spend.
-- The entity-bridge allocation introduces sub-percent gaps because UNKNOWN TSA categories
-- are excluded from entity_sl_mix, so per-entity SL percentages can sum to <100%.

SELECT
  health_system_name,
  total_spend,
  clinical_spend + nc_spend + pharma_spend + food_spend + exclude_spend + unmatched_spend AS sum_components,
  ABS(total_spend - (clinical_spend + nc_spend + pharma_spend + food_spend + exclude_spend + unmatched_spend)) AS gap,
  ROUND(SAFE_DIVIDE(
    ABS(total_spend - (clinical_spend + nc_spend + pharma_spend + food_spend + exclude_spend + unmatched_spend)),
    total_spend
  ) * 100, 3) AS gap_pct
FROM ${ref("wf_sl_v4")}
WHERE SAFE_DIVIDE(
    ABS(total_spend - (clinical_spend + nc_spend + pharma_spend + food_spend + exclude_spend + unmatched_spend)),
    total_spend
  ) > 0.001

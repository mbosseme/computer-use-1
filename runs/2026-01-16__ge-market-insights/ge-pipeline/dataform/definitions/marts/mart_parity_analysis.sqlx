js {
  const { buildCaseExpression } = require("../../includes/manufacturer_map");
  const manufacturerCase = buildCaseExpression("manufacturer_name");
}

config {
  type: "table",
  schema: "ge_sample_marts",
  tags: ["parity", "analysis"],
  description: "Comparison of aligned Transaction Analysis vs Supplier Spend data"
}

WITH transaction_aligned AS (
  SELECT
    FORMAT_DATE('%Y-Q%q', transaction_date) as year_quarter,
    CASE
      WHEN Contract_Category = 'COMPUTED TOMOGRAPHY' THEN 'CT'
      WHEN Contract_Category = 'MAGNETIC RESONANCE IMAGING' THEN 'MRI'
      WHEN Contract_Category = 'PHYSIOLOGICAL MONITORING SYSTEMS' THEN 'Monitoring'
      WHEN Contract_Category = 'ULTRASOUND RADIOLOGY CARDIOLOGY HAND CARRIED' THEN 'Ultrasound HC'
      ELSE 'Other'
    END as report_category,
    ${manufacturerCase} as manufacturer_normalized,
    SUM(spend) as transaction_spend
  FROM ${ref("stg_transaction_parity_basis")} t
  LEFT JOIN ${ref("int_parity_exclusion_list")} e
    ON t.Health_System_Entity_Code = e.Health_System_Entity_Code
    AND FORMAT_DATE('%Y-Q%q', t.transaction_date) = e.year_quarter
  WHERE e.Health_System_Entity_Code IS NULL
  GROUP BY 1, 2, 3
),

supplier_aligned AS (
  SELECT
    FORMAT_DATE('%Y-Q%q', spend_date) as year_quarter,
    CASE
      WHEN Contract_Category = 'COMPUTED TOMOGRAPHY' THEN 'CT'
      WHEN Contract_Category = 'MAGNETIC RESONANCE IMAGING' THEN 'MRI'
      WHEN Contract_Category = 'PHYSIOLOGICAL MONITORING SYSTEMS' THEN 'Monitoring'
      WHEN Contract_Category = 'ULTRASOUND RADIOLOGY CARDIOLOGY HAND CARRIED' THEN 'Ultrasound HC'
      ELSE 'Other'
    END as report_category,
    ${manufacturerCase} as manufacturer_normalized,
    SUM(spend) as supplier_spend
  FROM ${ref("stg_supplier_spend_parity")} s
  LEFT JOIN ${ref("int_facility_exclusion_list")} f
    ON s.Premier_Entity_Code = f.Premier_Entity_Code
    AND FORMAT_DATE('%Y-Q%q', s.spend_date) = f.year_quarter
  WHERE f.Premier_Entity_Code IS NULL
  GROUP BY 1, 2, 3
)

SELECT
  COALESCE(t.year_quarter, s.year_quarter) as year_quarter,
  COALESCE(t.report_category, s.report_category) as report_category,
  COALESCE(t.manufacturer_normalized, s.manufacturer_normalized) as manufacturer_normalized,
  COALESCE(t.transaction_spend, 0) as transaction_spend,
  COALESCE(s.supplier_spend, 0) as supplier_spend,
  COALESCE(t.transaction_spend, 0) - COALESCE(s.supplier_spend, 0) as spend_delta,
  SAFE_DIVIDE(COALESCE(t.transaction_spend, 0), COALESCE(s.supplier_spend, 0)) as transaction_to_supplier_ratio
FROM transaction_aligned t
FULL OUTER JOIN supplier_aligned s
  ON t.year_quarter = s.year_quarter
  AND t.report_category = s.report_category
  AND t.manufacturer_normalized = s.manufacturer_normalized
